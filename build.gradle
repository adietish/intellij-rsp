
plugins {
    id "org.jetbrains.intellij" version "0.4.10"
    id "com.adarshr.test-logger" version "1.7.0"
    id "idea"
    id "java"
    id "jacoco"
}

repositories {
    mavenCentral()
    maven {
        url 'https://repository.jboss.org'
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

intellij {
    version ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    def versions = ideaVersion.substring(3).split("\\.")
    if (versions[0] == "2020" || (versions[0] == "2019" && versions[1] != "1")) {
        plugins 'java', 'terminal'
    } else {
        plugins 'terminal'
    }
    pluginName 'org.jboss.tools.intellij.rsp'
}

publishPlugin {
    token    jetBrainsToken
    channels jetBrainsChannel
}

configurations {
    compile {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

dependencies {
    compile 'io.fabric8:openshift-client:4.9.1'
    compile 'io.fabric8:servicecatalog-client:4.9.1'
    compile 'org.apache.commons:commons-compress:1.19'
    compile 'org.apache.commons:commons-exec:1.3'
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'com.redhat.devtools.intellij:intellij-common:0.0.1-SNAPSHOT'
    compile group: 'org.eclipse.lsp4j', name: 'org.eclipse.lsp4j.jsonrpc', version: '0.9.0'
    compile 'org.jboss.tools.rsp.api.bundles:org.jboss.tools.rsp.api:0.22.9.Final@jar'
    testCompile 'org.mockito:mockito-core:2.23.4'
}

group 'org.jboss.tools.intellij'
version projectVersion // Plugin version
